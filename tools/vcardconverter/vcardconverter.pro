VERSION = 0.0.1
PROJECT_NAME = vcardconverter
TEMPLATE = app
CONFIG += ordered hide_symbols

equals(QT_MAJOR_VERSION, 4) {
    CONFIG += mobility
    MOBILITY += contacts versit

    DEFINES *= BEGIN_CONTACTS_NAMESPACE=QTM_BEGIN_NAMESPACE
    DEFINES *= END_CONTACTS_NAMESPACE=QTM_END_NAMESPACE
    DEFINES *= USE_CONTACTS_NAMESPACE=QTM_USE_NAMESPACE
}
equals(QT_MAJOR_VERSION, 5) {
    # Needed for qt4 moc, which can't handle numeric tests
    DEFINES *= QT_VERSION_5

    CONFIG += link_pkgconfig
    PKGCONFIG += Qt5Contacts Qt5Versit

    DEFINES *= USING_QTPIM
    DEFINES *= BEGIN_CONTACTS_NAMESPACE=QT_BEGIN_NAMESPACE_CONTACTS
    DEFINES *= END_CONTACTS_NAMESPACE=QT_END_NAMESPACE_CONTACTS
    DEFINES *= USE_CONTACTS_NAMESPACE=QTCONTACTS_USE_NAMESPACE
}


QT += gui
TARGET = $$PROJECT_NAME
CONFIG -= app_bundle # OS X

SOURCES += main.cpp photohandler.cpp
HEADERS += photohandler.h

target.path = $$INSTALL_ROOT/usr/bin/
INSTALLS += target
